static def getBuildVersion() {
    // min number required
    def version = 1
    def buildNumber = System.getenv("CIRCLE_BUILD_NUM")
    if (buildNumber != null) {
        version += buildNumber
    }

    println("VersionCode: " + version)
    return version
}

plugins {
    id 'com.android.application'
    id 'com.google.firebase.crashlytics'
    id 'com.github.triplet.play' version '2.4.2'
}

android {
    compileSdkVersion 33
    defaultConfig {
        versionCode getBuildVersion() as int
        versionName "2.5.0"

        minSdkVersion 24
        targetSdkVersion 33

        applicationId "com.syzible.irishnoungenders"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASSWORD")
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    buildFeatures {
        viewBinding = true
    }
    namespace 'com.syzible.irishnoungenders'
}

play {
    track = 'production'
    serviceAccountCredentials = file(System.getenv("PRIVATE_KEY") ?: "private_key.json")
    defaultToAppBundles = true
    // if the deployment fails due to an API error, don't fail the CI Build
    resolutionStrategy = "ignore"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.preference:preference:1.2.0'

    testImplementation 'junit:junit:4.13.2'

    implementation 'com.github.paolorotolo:appintro:v5.1.0'

    implementation 'com.google.firebase:firebase-analytics:21.3.0'
    implementation 'com.google.firebase:firebase-auth:22.0.0'

    implementation 'com.google.android.gms:play-services-auth:20.5.0'
    implementation 'com.google.android.gms:play-services-games:23.1.0'

    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.theah64.materialcolors:materialcolors:1.0.0'
    implementation 'com.hannesdorfmann.mosby3:viewstate:3.1.1'

    implementation 'com.loopj.android:android-async-http:1.4.9'

    implementation 'com.daimajia.easing:library:2.0@aar'
    implementation 'com.daimajia.androidanimations:library:2.3@aar'
}

apply plugin: 'com.google.gms.google-services'
